/*

Here we prove Theorems 5 and 6. It uses W/Q (Proposition 1) and F/Q(sqrt(5)) (Part (2) of Proposition 4).

*/

load "5-overQsqrt5.m";


print "We prove Theorem 5.";


print "";
print "We apply standard elimination with auxiliary primes 3, 7, 11, and 29 in the case 5 does not divide a + b and p >= 7 using F:";
print "(We note that only 3 and 7 are useful for p >= 11.)";
Bound(Nfs,FreyF,[3,7,11,29],7);
print "Done!";
print "There are 2 forms which are not eliminated (for any p) and four other forms which are eliminated only for p >= 11.";




print "";
print "We check that the two forms which are not eliminated correspond to twists of Frey curves F/Q(sqrt(5)) attached to trivial solutions.";

assert SameTraces(Nfs[19],FreyF(1,-1,1));
assert SameTraces(Nfs[21],FreyF(1,-1,2));


print "";
print "Done!";
print "This proves part (2) of Proposition 4.";

print "";
print "We check that the trace of Frobenius at 3 of these six newforms is 4.";

assert {TraceOfFrobenius(curve,I3) : curve in [FreyF(1,-1,1), FreyF(1,-1, 2)]} eq {4};
assert {HeckeEigenvalue(Nfs[i],I3) : i in [17,18,23,24]} eq {4};

print "Done!";
print "Therefore, since F = F_{a,b} has good reduction at 3, we have a_3(F) = 4 (mod p)."; 
print "We now compute the trace of Frobenius at 3 of F_{a,b}:";

for a,b in [0..2] do
    if [a,b] ne [0,0] then
        print "a =",a,"b =",b,"Trace of F at 3 =",TraceOfFrobenius(FreyF(a,b,1),I3);
    end if;
end for;

print "We see that F has trace 4 at 3 iff 3 | a +b.";
print "According to Lemma 1, this implies that W/Q has mutliplicative reduction at 3.";
print "Moreover, its mod p representation arises in level 2^4*5^2, 2^3*5^2, or 2*5^2.";



print "";
print "Modular forms in these levels all correspond to (isogeny classes of) elliptic curves over Q.";
print "We check that precisely 4 of them have trace of Frobenius at 3 which is -3 or +3.";
print "Finally, we also show that these four elliptic curves have potentially good reduction at 5 and that the valuation of their minimal discriminant at 5 is 2 or 8:";


for N in [2^4*5^2,2^3*5^2,2*5^2] do
    forms:=Newforms(CuspidalSubspace(ModularForms(Gamma0(N),2)));
    for i in [1..#forms] do
        f:=forms[i][1];
        if Coefficient(f,3)^2 eq 9 then
            F:=MinimalModel(EllipticCurve(f));
            print "Potentially good reduction at 5?", Valuation(jInvariant(F),5) gt 0, "Valuation of minimal disc at 5 =", Valuation(Discriminant(F),5);
        end if;
    end for;
end for;

print "";
print "This proves Theorem 5.";



print "";
print "We now prove Theorem 6.";
print "We denote by s a squareroot of 5.";



function InertiaTestM(a,b,d);
    print "*********************";
    print "Working with a =",a,"b =",b,"d =",d;
    Z:=FreyE(a,b,d);
    f3:=DivisionPolynomial(Z,3); 
    F2:=SplittingField(f3);

    R<y>:=PolynomialRing(F2);
    LI:=Factorization(R!f3);

    L:=F2;
    for i in [1..#LI] do
        r:=-Coefficient(LI[1][1],0);
        R<y>:=PolynomialRing(L);
        g:=R!Evaluate(DefiningPolynomial(Z),[r,y,1]);
        if IsIrreducible(g) then
            L:=ext<L|g>;
        end if;
    end for;

    print "The degree of the 3-division field of Z = E_{a,b}^{(d)} is", AbsoluteDegree(L)/Degree(K);

    f3a:=Factorization(f3)[1][1];
    F2<x>:=NumberField(f3a);
    R<y>:=PolynomialRing(F2);
    M:=ext<F2|Evaluate(DefiningPolynomial(Z),[x,y,1])>;

    print "We choose a 3-torsion point whose coordinates (x,y) satisfy:";
    print "Defining polynomial for the x-coordinate:",f3a;
    print "Defining polynomial for the y-coordinate:", Evaluate(DefiningPolynomial(Z),[x,y,1]);
    print "Degree over Q(sqrt5) of the field M generated by x and y is:", AbsoluteDegree(M)/Degree(K);
    
    OM:=Integers(M);
    J:=Factorization(2*OM);
    assert #J eq 1;
    print "";
    print "There is a unique prime above 2 in M.";

    J2:=J[1][1];

    print "";
    print "It is totally ramified. We call it Q'.";

    // test if the elliptic curve Z has good reduction over M
    assert LocalInformation(BaseChange(Z,M),J2)[3] eq 0;
    print "";
    print "The elliptic curve Z = E(a,b)^{(d)} has good reduction over M.";

    S:={};
    print "";
    print "We display below a, b (within {1,..8}, not both even and such that 8 divides a + b), the valuation of the conductor of E(a,b) base changed to M at Q' and the Kodaira Symbol.";
    for a,b in [1..2^3] do
        if [a mod 2, b mod 2] ne [0,0] and (a+b) mod 8 eq 0 then
            E1:=FreyE(a,b,1);
            LI:=LocalInformation(BaseChange(E1,M),J2);
            L1:=LI[3]; 
            print "a =",a,"b =",b,"val =",L1, "Kodaira type", LI[5];
            S:=S join {L1};
        end if;
    end for;
    print "";
    return S;
end function;

assert InertiaTestM(1,1,1) ne {0};
assert InertiaTestM(1,1,2) ne {0};
assert InertiaTestM(1,0,1) ne {0};
assert InertiaTestM(1,0,-1) ne {0};
assert InertiaTestM(1,0,2) ne {0};
assert InertiaTestM(1,0,-2) ne {0};

print "This proves Proposition 6 and hence Theorem 6.";

